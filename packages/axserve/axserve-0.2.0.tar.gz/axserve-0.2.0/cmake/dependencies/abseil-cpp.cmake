cmake_minimum_required(VERSION 3.27)

include_guard(GLOBAL)

message(CHECK_START "Checking Abseil C++ provider")

if(AXSERVE_ABSL_PROVIDER STREQUAL "module")
    set(ABSL_EXTERNAL_NAME "absl")
    set(ABSL_PREFIX_NAME "absl")
    set(ABSL_THIRD_PARTY_NAME "abseil-cpp")

    set(ABSL_GIT_REPOSITORY "https://github.com/abseil/abseil-cpp.git")
    set(ABSL_GIT_TAG "20250512.1")

    include("${CMAKE_CURRENT_LIST_DIR}/googletest.cmake")

    set(ABSL_CMAKE_CACHE_ARGS
        "-DCMAKE_SYSTEM_NAME:STRING=${CMAKE_SYSTEM_NAME}"
        "-DCMAKE_SYSTEM_VERSION:STRING=${CMAKE_SYSTEM_VERSION}"
        "-DCMAKE_SYSTEM_PROCESSOR:STRING=${CMAKE_SYSTEM_PROCESSOR}"
        "-DCMAKE_TOOLCHAIN_FILE:FILEPATH=${CMAKE_TOOLCHAIN_FILE}"
        "-DCMAKE_CXX_STANDARD:STRING=${CMAKE_CXX_STANDARD}"
        "-DCMAKE_CXX_STANDARD_REQUIRED:BOOL=${CMAKE_CXX_STANDARD_REQUIRED}"
        "-DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}"
        "-DBUILD_TESTING:BOOL=${BUILD_TESTING}"
        "-DABSL_BUILD_TESTING:BOOL=${BUILD_TESTING}"
        "-DABSL_BUILD_TEST_HELPERS:BOOL=${BUILD_TESTING}"
        "-DABSL_ENABLE_INSTALL:BOOL=ON"
        "-DABSL_PROPAGATE_CXX_STD:BOOL=ON"
        "-DABSL_USE_GOOGLETEST_HEAD:BOOL=OFF"
        "-DABSL_USE_EXTERNAL_GOOGLETEST:BOOL=ON"
        "-DABSL_FIND_GOOGLETEST:BOOL=ON"
    )

    if(AXSERVE_GTEST_PROVIDER STREQUAL "module")
        list(APPEND ABSL_DEPENDS GTest)
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/../util/external-project.cmake")

    ExternalProject_AddForThisProject("${ABSL_EXTERNAL_NAME}"
        PREFIX_NAME "${ABSL_PREFIX_NAME}"
        THIRD_PARTY_NAME "${ABSL_THIRD_PARTY_NAME}"
        GIT_REPOSITORY "${ABSL_GIT_REPOSITORY}"
        GIT_TAG "${ABSL_GIT_TAG}"
        LOG_CONFIGURE TRUE
        LOG_BUILD TRUE
        LOG_OUTPUT_ON_FAILURE TRUE
        CMAKE_CACHE_ARGS ${ABSL_CMAKE_CACHE_ARGS}
        DEPENDS ${ABSL_DEPENDS}
    )

    message(CHECK_PASS "${AXSERVE_ABSL_PROVIDER}")
elseif(AXSERVE_ABSL_PROVIDER STREQUAL "package")
    find_package(absl CONFIG REQUIRED)
    message(CHECK_PASS "${AXSERVE_ABSL_PROVIDER}")
else()
    message(CHECK_FAIL "${AXSERVE_ABSL_PROVIDER}")
endif()
