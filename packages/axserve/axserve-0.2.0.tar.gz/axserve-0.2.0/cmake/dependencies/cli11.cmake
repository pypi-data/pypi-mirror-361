cmake_minimum_required(VERSION 3.27)

include_guard(GLOBAL)

message(CHECK_START "Checking CLI11 provider")

if(AXSERVE_CLI11_PROVIDER STREQUAL "module")
    set(CLI11_EXTERNAL_NAME "CLI11")
    set(CLI11_PREFIX_NAME "cli11")
    set(CLI11_THIRD_PARTY_NAME "CLI11")

    set(CLI11_GIT_REPOSITORY "https://github.com/CLIUtils/CLI11.git")
    set(CLI11_GIT_TAG "v2.5.0")

    set(CLI11_CMAKE_CACHE_ARGS
        "-DCMAKE_SYSTEM_NAME:STRING=${CMAKE_SYSTEM_NAME}"
        "-DCMAKE_SYSTEM_VERSION:STRING=${CMAKE_SYSTEM_VERSION}"
        "-DCMAKE_SYSTEM_PROCESSOR:STRING=${CMAKE_SYSTEM_PROCESSOR}"
        "-DCMAKE_TOOLCHAIN_FILE:FILEPATH=${CMAKE_TOOLCHAIN_FILE}"
        "-DCMAKE_CXX_STANDARD:STRING=${CMAKE_CXX_STANDARD}"
        "-DCMAKE_CXX_STANDARD_REQUIRED:BOOL=${CMAKE_CXX_STANDARD_REQUIRED}"
        "-DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}"
    )

    include("${CMAKE_CURRENT_LIST_DIR}/../util/external-project.cmake")

    ExternalProject_AddForThisProject("${CLI11_EXTERNAL_NAME}"
        PREFIX_NAME "${CLI11_PREFIX_NAME}"
        THIRD_PARTY_NAME "${CLI11_THIRD_PARTY_NAME}"
        GIT_REPOSITORY "${CLI11_GIT_REPOSITORY}"
        GIT_TAG "${CLI11_GIT_TAG}"
        CMAKE_CACHE_ARGS ${CLI11_CMAKE_CACHE_ARGS}
        DEPENDS ${CLI11_DEPENDS}
        START_UNPARSED_ARGUMENTS
        LOG_CONFIGURE TRUE
        LOG_BUILD TRUE
        LOG_OUTPUT_ON_FAILURE TRUE
    )

    message(CHECK_PASS "${AXSERVE_CLI11_PROVIDER}")
elseif(AXSERVE_CLI11_PROVIDER STREQUAL "package")
    find_package(CLI11 CONFIG REQUIRED)
    message(CHECK_PASS "${AXSERVE_CLI11_PROVIDER}")
else()
    message(CHECK_FAIL "${AXSERVE_CLI11_PROVIDER}")
endif()
